#version 430
layout ( binding = 0, r32f ) readonly uniform image2D inImg;
layout ( binding = 1, r32f ) writeonly uniform image2D outImg;

layout (local_size_x = 1, local_size_y=1, local_size_z=1) in;


void main() {
	
	float[8] temp;
	
	float tmp0, tmp1, tmp2, tmp3, tmp4, tmp5, tmp6, tmp7;
	float tmp10, tmp11, tmp12, tmp13;
	float z5, z10, z11, z12, z13;
	
	ivec2 index = ivec2 (gl_GlobalInvocationID.x, gl_GlobalInvocationID.y*8);
	
	for(int i = 0; i<8; i++){
		temp[i] = imageLoad(inImg, ivec2(index.x, index.y + i)).x / 8.0;
	}
	
    /* Even part */
    tmp0 = temp[0];
    tmp1 = temp[2];
    tmp2 = temp[4];
    tmp3 = temp[6];

    tmp10 = tmp0 + tmp2;	/* phase 3 */
    tmp11 = tmp0 - tmp2;

    tmp13 = tmp1 + tmp3;	/* phases 5-3 */
    tmp12 = (tmp1 - tmp3) * 1.414213562 - tmp13; /* 2*c4 */

    tmp0 = tmp10 + tmp13;	/* phase 2 */
    tmp3 = tmp10 - tmp13;
    tmp1 = tmp11 + tmp12;
    tmp2 = tmp11 - tmp12;
    
    /* Odd part */
    tmp4 = temp[1];
    tmp5 = temp[3];
    tmp6 = temp[5];
    tmp7 = temp[7];

    z13 = tmp6 + tmp5;		/* phase 6 */
    z10 = tmp6 - tmp5;
    z11 = tmp4 + tmp7;
    z12 = tmp4 - tmp7;

    tmp7 = z11 + z13;		/* phase 5 */
    tmp11 = (z11 - z13) * 1.414213562; /* 2*c4 */

    z5 = (z10 + z12) * 1.847759065; /* 2*c2 */
    tmp10 = 1.082392200 * z12 - z5; /* 2*(c2-c6) */
    tmp12 = -2.613125930 * z10 + z5; /* -2*(c2+c6) */

    tmp6 = tmp12 - tmp7;	/* phase 2 */
    tmp5 = tmp11 - tmp6;
    tmp4 = tmp10 + tmp5;
    
    imageStore(outImg, ivec2(index.x, index.y + 0), vec4(tmp0 + tmp7, 0, 0, 0));
    imageStore(outImg, ivec2(index.x, index.y + 7), vec4(tmp0 - tmp7, 0, 0, 0));
    imageStore(outImg, ivec2(index.x, index.y + 1), vec4(tmp1 + tmp6, 0, 0, 0));
    imageStore(outImg, ivec2(index.x, index.y + 6), vec4(tmp1 - tmp6, 0, 0, 0));
    imageStore(outImg, ivec2(index.x, index.y + 2), vec4(tmp2 + tmp5, 0, 0, 0));
    imageStore(outImg, ivec2(index.x, index.y + 5), vec4(tmp2 - tmp5, 0, 0, 0));
    imageStore(outImg, ivec2(index.x, index.y + 4), vec4(tmp3 + tmp4, 0, 0, 0));
    imageStore(outImg, ivec2(index.x, index.y + 3), vec4(tmp3 - tmp4, 0, 0, 0));
	
}